 /* Runs ok 2003-07-01 */
 /* 1-wrap Connect code
  * 2-noERRORABEND
  * 3-%let TMPF='SASAF';
  * 4-%let LO=000000;
  * 5-%s/IN/'BF19.NJX0213.MORMER'/  <---s/b 2 substitutions
  */
options ls=255;
%include "&HOME/code/sas/connect_setup.sas";
signon cdcjes2;
rsubmit;

%let TMPF='SASAF';
%let LO=000000;

OPTIONS LS=133 PS=61 SOURCE NOTES CAPS noERRORABEND FILEMSGS;
OPTIONS MAUTOSOURCE SASAUTOS ='DWJ2.MACROS.LIB';

* THIS VERSION COPIED 29 MAY 2003;

* PROGRAM NAME  MORRES02;

* THIS PROGRAM WILL READ THE SPECIFIED BF19 MORTALITY FILE AND;
* AFTER CONVERTING CERTAIN DATA FIELDS PERFORM A PROC FREQ ON;
* ALL GEOGRAPHIC ITEMS AND WRITE THE PROC FREQ RESULTS TO THE;
* MORTALITY GEOG SAS DATA LIBRARIES;

* UPDATE LOG;
* 02/08/99 UPDATED THE PROGRAM TO MEEK Y2K SPECIFICATIONS;
* 08/19/99 MODIFIED CODE SUCH THAT THE STATISTICIANS CANNOT;
*          UPDATE THE SAS FILES WHEN RUNNING THIS PROGRAM;
*          FROM THE AF APPLICATION;
* 08/30/99 MODIFIED CODE TO ACCEPT THE ASSIGNED COUNTY CODE OF;
*          701 FOR THE STATE OF AMERICAN SAMOA AND NORTHERN MARIANAS;
*          THIS CODE WAS ASSIGNED BY TERRY AND BRENDA;
* 01/21/00 REVISED CODE TO PROCESS YEAR 2000 FILES;
*          CHANGED THE MACRO VARIABLE I TO 2000;
* 02/29/00 ADDED THE WAIT OPTION IN THE LIBNAME STATEMENT;
*          TO CORRECT THE USER ALLOCATION PROBLEM;
* 12/12/00 (TLL) REVISED CODE TO PROCESS YEAR 2000 FILES;
*          CHANGED THE MACRO VARIABLE I TO 2001;
*          CHANGED THE WAIT OPTION FROM 30 TO 45;
* 01 MAR 2001 (KJK) CHANGED THE WAIT OPTION FROM 45 TO 60;
* 11 JUL 2001 (KJK) CHANGED THE WAIT OPTION FROM 60 TO 120;
* 06 DEC 2001 (KJK) CREATED YEAR 2002 VERSION OF PROGRAM;
* 01 JUL 2002 (KJK) ADDED OPTION TO LIMIT REPORT TO RANGE OF;
*                   CERTIFICATE NUMBERS;
* 17 JAN 2003 (KJK) REVISED PROGRAM TO HANDLE UPDATED GEO TABLES;
* 29 MAY 2003 (KJK) MADE CHANGES TO OUT-OF-STATE RESIDENCE;
*                   REPORT TO PROVIDE COMPARABLE DATA TO 2003;
**************************************************************;
%GLOBAL S I N TMPF HI LO;

* INPUT THE YEAR TO PROCESS;
%LET I = 2002;

DATA _NULL_;
 INFILE 'BF19.NJX0213.MORMER' OBS=1;
 INPUT @77 STATE $2.;
 CALL SYMPUT('N',STATE);
RUN;

* MACRO STATES RETURNS THE STATE ABBRE AND STATE NAME;
%STATES

*WHEN RUNNING THIS PROGRAM FROM THE AF APPLICATION;
*THE MACRO VARIABLE TMPF IS SET TO SASAF AND;
*YOU ARE ASSIGNED THE TEMP LIBRARIES;
%MACRO DATASET;
 %IF &TMPF = 'SASAF' %THEN
  %DO;
LIBNAME MOR&I "DWJ2.RESIDE.MOR&I..LIBRARY.TEMP" DISP=OLD WAIT=250;
LIBNAME MOST&I "DWJ2.RESIDE.MOR&I..STATE.LIBRARY.TEMP"
DISP=OLD WAIT=250;
LIBNAME MORC&I "DWJ2.RESIDE.MOR&I..COUNTY.LIBRARY.TEMP"
DISP=OLD WAIT=250;
LIBNAME MOOS&I "DWJ2.RESIDE.MOR&I..OS.LIBRARY.TEMP" DISP=OLD WAIT=250;
  %END;
 %ELSE
  %DO;
LIBNAME MOR&I "DWJ2.RESIDE.MOR&I..LIBRARY" DISP=OLD WAIT=250;
LIBNAME MOST&I "DWJ2.RESIDE.MOR&I..STATE.LIBRARY" DISP=OLD WAIT=250;
LIBNAME MORC&I "DWJ2.RESIDE.MOR&I..COUNTY.LIBRARY" DISP=OLD WAIT=250;
LIBNAME MOOS&I "DWJ2.RESIDE.MOR&I..OS.LIBRARY" DISP=OLD WAIT=250;
  %END;
%MEND DATASET;

%DATASET

* READ THE BF19 FILE INTO A SAS FILE;
   DATA RES;
    INFILE 'BF19.NJX0213.MORMER';
    INPUT @5 CERTNO $6. @47 ALIAS $1. @77 OCCUR $5. @89 RESID $5.;

    IF ALIAS = '1' THEN DELETE;

* THIS MACRO RESTRICTS THE RANGE OF CERTIFICATES TO BE REPORTED ON;
* IF SELECTED WITHIN THE FCAST SYSTEM;
%MACRO RESTRICT;
%IF &TMPF = 'SASAF' %THEN %DO;
 %IF &LO NE 000000 %THEN %DO;
  IF CERTNO LT &LO OR CERTNO GT &HI THEN DELETE;
  %END;
 %END;
%MEND RESTRICT;

%RESTRICT

* PROCESS THE SAS FILE;
  DATA ALL (KEEP=OCCUR CODE STATE COOCC RSTATE RCOUNTY);
    LENGTH RSTATE STATE $2 COOCC RCOUNTY $3 CODE $5;
    SET RES;

    CODE=RESID;
    STATE=SUBSTR(OCCUR,1,2);
    COOCC=SUBSTR(OCCUR,3,3);
    RSTATE=SUBSTR(CODE,1,2);
    RCOUNTY=SUBSTR(CODE,3,3);

* THE FOLLOWING SPECIAL CODE IS FOR NEW YORK CITY ONLY;
* CHECK THE CORES AND CONVERT TO YC IF APPROPRIATE;
    IF CODE ='33010' THEN CODE = '55010';
    IF CODE ='33011' THEN CODE = '55011';
    IF CODE ='33053' THEN CODE = '55053';
    IF CODE ='33077' THEN CODE = '55077';
    IF CODE ='33087' THEN CODE = '55087';
RUN;

PROC SORT DATA=ALL OUT=ALL;
 BY RSTATE;
RUN;

PROC FREQ DATA=ALL;
 BY RSTATE;
 TABLES CODE / MISSING NOPRINT OUT=MOR&I..&S&I;
RUN;

PROC FREQ DATA=ALL;
 TABLES RSTATE / MISSING NOPRINT OUT=MOST&I..&S&I;
RUN;

* READ THE FIRST WORK DATA SET CREATED AND DELETE ALL OF;
* RECORDS THAT ARE EQUAL TO THE STATE SELECTED;
* PERFORM PROC FREQ BY RCOUNTY TO CREATE THE OUT OF STATE;
* RESIDENCE REPORT;
DATA ALLC;
 SET ALL;
* GU, MP, AND AS DO NOT HAVE COUNTY CODES, DUMMY CODE CREATED;
 IF STATE IN ('54','61','62') THEN
      DO;
        RCOUNTY = '000';
        ASSIGN = 3;
      END;
* THIS DATASET MAY NOT APPLY TO VIRGIN ISLANDS SO CREATE A
* DUMMY RECORD IF NECESSARY;
 ELSE IF STATE = '53' THEN
      DO;
        IF RSTATE = '53' THEN ASSIGN = 3;
        ELSE IF RSTATE NE '53' THEN
           DO;
             IF RCOUNTY LT '700' THEN ASSIGN=1;
             IF RCOUNTY GE '700' THEN ASSIGN=2;
             IF RCOUNTY EQ '   ' THEN ASSIGN=3;
           END;
      END;
 ELSE
     DO;
       IF RSTATE EQ &N THEN DELETE;
       IF RCOUNTY LT '700' THEN ASSIGN=1;
       IF RCOUNTY GE '700' THEN ASSIGN=2;
       IF RCOUNTY EQ '   ' THEN ASSIGN=3;
     END;
RUN;

PROC FREQ DATA=ALLC;
  TABLES ASSIGN / MISSING NOPRINT OUT=MOOS&I..&S&I;
RUN;

* READ THE FIRST WORK DATA SET CREATED AND DELETE ALL OF;
* RECORDS THAT ARE NOT EQUAL TO THE STATE SELECTED;
* PERFORM PROC FREQ BY RCOUNTY TO CREATE THE COUNTY OF;
* OCCURRENCE REPORT;
DATA ALLC;
 SET ALL;

 IF STATE NE &N THEN DELETE;

* RETAIN ALL CODES ABOVE 700 AS WELL AS THE FOLLOWING SPECIAL;
* CASES, PLUS VALID CODES FOR YC, GU, AS, AND MP;
 IF 0 LT COOCC LT 700 THEN DO;
  IF OCCUR NOTIN (
    '02001',
    '02003',
    '05236',
    '06009',
    '09001',
    '18016',
    '19022',
    '21003',
    '22012',
    '22019',
    '22072',
    '22097',
    '26048',
    '27001',
    '29002',
    '39092',
    '47001',
    '47002',
    '47004',
    '47005',
    '47006',
    '47008',
    '47009',
    '47010',
    '47011',
    '47013',
    '47014',
    '47016',
    '47018',
    '47019',
    '47020',
    '47021',
    '47022',
    '47023',
    '47024',
    '47025',
    '47026',
    '47027',
    '47028',
    '47029',
    '47030',
    '47031',
    '47033',
    '47034',
    '47035',
    '47036',
    '54   ',
    '54000',
    '55010',
    '55011',
    '55053',
    '55077',
    '55087',
    '61   ',
    '61000',
    '62   ',
    '62000')
   THEN DELETE;
  END;

 RCOUNTY = COOCC;  /* NECESSARY TO PRESERVE DATASET VARS */

 PROC FREQ DATA=ALLC;
   TABLES RCOUNTY / MISSING NOPRINT OUT=MORC&I..&S&I;
 RUN;


endrsubmit;
signoff cdcjes2;
