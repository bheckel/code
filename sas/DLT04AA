 filename IN 'BQH0.DEX0412.MEDMER';
 /**********************************************************************
  * PROGRAM NAME  DLT04AA
  *               (OLD NAME CAUSE04)
  *
  * DESCRIPTION   UPDATES A STATE'S 2004 UNDERLYING CAUSE DATASET
  *               LIBRARY
  *
  * CALLED BY     FCAST, SMEDCPS AND SMEDCPSP JOBSTREAMS
  * CALLS TO
  *
  * INPUT MVARS   TMPF 
  *
  * PROGRAMMER    <SEVERAL>
  * DATE WRITTEN  23 DEC 2003
  *
  * UPDATE LOG
  * 08/17/1999 UPDATED THE PROGRAM TO KEEP THE STATISTICIANS
  *            FROM UPDATING THE SAS DATASETS
  * 01/20/2000 CREATED PROG FOR DATA YEAR 2000
  * 02/29/00  ADDED THE WAIT OPTION IN THE LIBNAME STATEMENT
  *           TO CORRECT THE USER ALLOCATION PROBLEM
  * 12/06/2002(ZQQ8)MODIFIED PROGRAM FOR 2002 DATA YEAR
  * 01/02/2003 COPIED CAUSE02 AS CAUSE03 FOR 2003 DATA YEAR,
  *            NO CODE CHANGES REQUIRED (BQH0)
  * 03/20/2003 ADJUSTED COLUMN POSITION FOR CAUSE VARIABLE (BQH0) 
  * 04/07/2003 (BQH0) REMOVED UNUSED VARIABLES, MODIFIED THE 
  *            DATA _NULL_ STEP TO CHECK FOR VALID STATE NAMES 
  *            AND ELIMINATED EXTERNAL MACRO CALLS TO 
  *            ACCOMODATE MEDICAL'S CHANGE FROM NUMERIC TO ALPHA  
  *            STYLE STATE CODES IN 2003. 
  * 24 DEC 2003 (BQH0) CREATED YEAR 2004 VERSION OF PROGRAM
  ********************************************************************/
OPTIONS LS=135 PS=61 SOURCE NOTES CAPS noERRORABEND;
OPTIONS MAUTOSOURCE SASAUTOS ='DWJ2.MACROS.LIB';

%GLOBAL ST YEAR TMPF;

 /* MEDICAL HAS NO ERROR CHECKING ON THEIR FILENAMES.  SO WE'RE
  * PARSING THE FILENAME TO DETERMINE THE STATE NAME AND ABORTING
  * IF IT IS NOT ONE OF THE KNOWN 57.
  */
DATA _NULL_;
  LENGTH NAME $35;
  INFILE IN FILENAME=NAME OBS=1;
  NAME=SUBSTR(NAME, 6, 2);

  IF NAME NOT IN('AK', 'AL', 'AR', 'AS', 'AZ', 'CA', 'CO', 'CT',
                 'DC', 'DE', 'FL', 'GA', 'GU', 'HI', 'IA', 'ID',
                 'IL', 'IN', 'KS', 'KY', 'LA', 'MA', 'MD', 'ME',
                 'MI', 'MN', 'MO', 'MP', 'MS', 'MT', 'NC', 'ND',
                 'NE', 'NH', 'NJ', 'NM', 'NV', 'NY', 'OH', 'OK',
                 'OR', 'PA', 'PR', 'RI', 'SC', 'SD', 'TN', 'TX',
                 'UT', 'VA', 'VI', 'VT', 'WA', 'WI', 'WV', 'WY',
                 'YC') THEN
    ABORT ABEND;
RUN;


DATA UC;
  INFILE IN;
  /* MEDICAL IS USING 2 DIGIT ALPHA CHARACTER INSTEAD OF 2 DIGIT NUMERIC
   * CODES STARTING IN 2003.  E.G. NC FOR A NORTH CAROLINA MEDMER FILE.
   */
  INPUT @1 YR $CHAR4.  @5 STATEALPHA $CHAR2.  @51 CAUSE $CHAR5.;
  IF YR NE '' THEN
    CALL SYMPUT('YEAR', YR);
  IF STATEALPHA NE "" THEN
    CALL SYMPUT('ST', STATEALPHA);
RUN;


PROC SORT DATA=UC;
  BY CAUSE;
RUN;


 /* CHECK TO SEE IF THIS PROGRAM WAS RUN VIA THE AF APPLICATION.  IF SO,
  * USE A TEMPORARY DATA LIBRARY.
  */
%MACRO DATASET;
  %IF &TMPF = 'SASAF' %THEN
   %DO;
     LIBNAME CAUS&YEAR "DWJ2.CAUS&YEAR..LIBRARY.TEMP" DISP=OLD WAIT=30;
   %END;
  %ELSE
   %DO;
     LIBNAME CAUS&YEAR "DWJ2.CAUS&YEAR..LIBRARY" DISP=OLD WAIT=30;
   %END;
%MEND DATASET;
%DATASET


PROC FREQ DATA=UC;
  TABLES CAUSE / MISSING NOPRINT OUT=CAUS&YEAR..&ST&YEAR;
RUN;
