%let cdcjes2=158.111.2.21 ;
filename rlink 'k:\everyone\signon.tcp';
options comamid=tcp remote=cdcjes2 ;

data _NULL_ ; 
  length userid $ 4 passwd $ 8;
  userid = sysget('USERNAME');
passwd='capital';
  if userid = '' then
    userid = 'XXX#';
  window Login  icolumn=10 columns=50 irow=10 rows=10
    #2 @10 'UserID: ' userid
    #4 @10 'Enter Mainframe Password: ' passwd  display=no required=yes;
  display Login;
  call symput('USERID', userid);
  call symput('PASSWD', passwd);
  stop;
run;

signon cdcjes2;


options ls=133 notes source mprint symbolgen;
* Program Name  convke3 03/17/2000 Brenda Boswell;
*
* This program will convert the key entry iii program into a;
* a file that can be read into VSCPSAMP;
*
* Update Log;
* 2003-04-25 (BQH0) Major modifications to: ;
*            - Handle 2 dataline input instead of 1;
*            - Modify to detect fips/multirace states;
*              while still handling 2002 and earlier states;
*            - Improve login process;
* 2003-05-08 (BQH0) Numeric state wanted in old occ state position and;
*            alpha state wanted in FIPS occ state position to;
*            accomodate VSCPSAMP;

 /* Local file generated by KE3: */
/* DEBUG */
***filename DATAIN "K:\KE3DATA\ALL\MORDATA.CMB" LRECL=454;
filename DATAIN "mordata.cmb" LRECL=454;


rsubmit;


options ls=133 notes source mprint symbolgen;
 /* Remote temporary working file to hold KE3 data: */
/* DEBUG */
***filename SHAREBF "BF19.MORTAL2.TEMPFILE" LRECL=454 DISP=SHR;
filename SHAREBF "BQH0.MORTAL2.TEMPFILE" LRECL=454 DISP=SHR;

proc upload infile=DATAIN outfile=SHAREBF;
run;


 /* Detect old or new FIPS (2003+) style data based on whether the FIPS birth
  * country field is alpha or numeric. 
  */
data _NULL_;
  %global IS_ALPHA;
  infile SHAREBF MISSOVER;
  input #1  bcountryFIPSTMP $ 143-144;

  /* Prevent missings from evaluating as alphas. */
  if bcountryFIPSTMP = '' then
    bcountryFIPSTMP = '0';

  isalpha = (NOT verify(compress(upcase(bcountryFIPSTMP)), 
                                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'));
  if isalpha > 0 then
    call symput('IS_ALPHA', 'yes');
  else
    call symput('IS_ALPHA', 'no');

  /* Want to capture the first birth country found, if any, so look at the
   * first KE3 line as a (hopefully) representative sample. 
   */
  if  _N_ > 0 then
    stop;
run;


 /* Detect old or new Multi-race (2003+) style data based on whether
  * the first multi-race KE3 field is blank or not.
  */
data _NULL_;
  infile SHAREBF MISSOVER;
  input #2  multiTMP $ 166-167;

  if multiTMP eq '' then
    call symput('IS_MULTI', 'no');
  else
    call symput('IS_MULTI', 'yes');

  if  _N_ > 0 then
    stop;
run;


 /* Input KE3 datafile per the specifications.  Two datalines per record. */
data work.trial;
  infile SHAREBF;
  length statenum $2;
  input #1  shipno $ 1-2  certno $ 5-10  sex $ 48  dmo $ 49-50 
        dday $ 51-52  age $ 64-66  birthmo $ 67-68  birthdy $ 69-70 
        bstate $ 74-75  typlac $ 76  stocc $ 77-78  dcounty $ 79-81 
        dyear $ 135-138  byear $ 139-142  bcountryFIPS $ 143-144
        bstateFIPS $ 145-146  dcountyFIPS $ 149-151

        #2  marital $ 82  stres $ 89-90  cntyres $ 91-93  hispanic $ 94  
        race $ 95  educ $ 96-97  injury $ 117  countryresFIPS $ 152-153
        stateresFIPS $ 154-155  countyresFIPS $ 156-158  
        placeresFIPS $ 159-163  mr1 $ 166-167  mr2 $ 168-169  mr3 $ 170-171
        mr4 $ 172-173  mr5 $ 174-175  mr6 $ 176-177  mr7 $ 178-179
        mr8 $ 180-181  mr9 $ 182-183  mr10 $ 184-185  mr11 $ 186-187
        mr12 $ 188-189  mr13 $ 190-191  mr14 $ 192-193  mr15 $ 194-195 
        mr16 $ 196-215  mr17 $ 216-235  mr18 $ 236-255  mr19 $ 256-275
        mr20 $ 276-295  mr21 $ 296-315  mr22 $ 316-335  mr23 $ 336-355
        ;

  /* Save original numeric value.  Always want a numeric state code for the
   * filename and for output position 77 to keep VSCPSAMP happy...
   */
  statenum=stocc;
  /* ...but want alpha state code for the file if we're working on a FIPS
   * state. 
   */
  if "&IS_ALPHA" eq 'yes' then
    do;
      select ( stocc );
        when ('01') stocc = 'AL';
        when ('02') stocc = 'AK';
        when ('03') stocc = 'AZ';
        when ('04') stocc = 'AR';
        when ('05') stocc = 'CA';
        when ('06') stocc = 'CO';
        when ('07') stocc = 'CT';
        when ('08') stocc = 'DC';
        when ('09') stocc = 'DE';
        when ('10') stocc = 'FL';
        when ('11') stocc = 'GA';
        when ('12') stocc = 'HI';
        when ('13') stocc = 'ID';
        when ('14') stocc = 'IL';
        when ('15') stocc = 'IN';
        when ('16') stocc = 'IA';
        when ('17') stocc = 'KS';
        when ('18') stocc = 'KY';
        when ('19') stocc = 'LA';
        when ('20') stocc = 'ME';
        when ('21') stocc = 'MD';
        when ('22') stocc = 'MA';
        when ('23') stocc = 'MI';
        when ('24') stocc = 'MN';
        when ('25') stocc = 'MS';
        when ('26') stocc = 'MO';
        when ('27') stocc = 'MT';
        when ('28') stocc = 'NE';
        when ('29') stocc = 'NV';
        when ('30') stocc = 'NH';
        when ('31') stocc = 'NJ';
        when ('32') stocc = 'NM';
        when ('33') stocc = 'NY';
        when ('34') stocc = 'NC';
        when ('35') stocc = 'ND';
        when ('36') stocc = 'OH';
        when ('37') stocc = 'OK';
        when ('38') stocc = 'OR';
        when ('39') stocc = 'PA';
        when ('40') stocc = 'RI';
        when ('41') stocc = 'SC';
        when ('42') stocc = 'SD';
        when ('43') stocc = 'TN';
        when ('44') stocc = 'TX';
        when ('45') stocc = 'UT';
        when ('46') stocc = 'VT';
        when ('47') stocc = 'VA';
        when ('48') stocc = 'WA';
        when ('49') stocc = 'WV';
        when ('50') stocc = 'WI';
        when ('51') stocc = 'WY';
        when ('52') stocc = 'PR';
        when ('53') stocc = 'VI';
        when ('54') stocc = 'GU';
        when ('55') stocc = 'YC';
        when ('99') stocc = '99';
        otherwise stocc = '??';
      end;
    end;

  /* Want to convert multirace codes into Y or N checkboxes if we're working
   * on a multirace state. 
   */
  if "&IS_MULTI" eq 'yes' then
    do;
      /* From the KE3 coder's input (e.g. 03, 34, etc.): */
      array mrarr[*] $2 mr1-mr15;
      /* Declaration for NCHS formatting (holds Y or N, defined below): */
      array mrckboxarr[*] $1 mrckbox1-mrckbox15;
      /* Initialize all to the default 'No' checkbox. */
      do i=1 to hbound(mrckboxarr);
        mrckboxarr[i] = 'N';
      end;
      /* Then set the Yes' where needed. */
      do i=1 to hbound(mrarr);
        select ( mrarr[i] );
          when ('01') mrckboxarr[1] = 'Y';
          when ('02') mrckboxarr[2] = 'Y';
          when ('03') mrckboxarr[3] = 'Y';
          when ('04') mrckboxarr[4] = 'Y';
          when ('05') mrckboxarr[5] = 'Y';
          when ('06') mrckboxarr[6] = 'Y';
          when ('07') mrckboxarr[7] = 'Y';
          when ('08') mrckboxarr[8] = 'Y';
          when ('09') mrckboxarr[9] = 'Y';
          when ('10') mrckboxarr[10] = 'Y';
          when ('11') mrckboxarr[11] = 'Y';
          when ('12') mrckboxarr[12] = 'Y';
          when ('13') mrckboxarr[13] = 'Y';
          when ('14') mrckboxarr[14] = 'Y';
          when ('15') mrckboxarr[15] = 'Y';
          otherwise;
        end;
      end;
    end;

  %global USER CTIME DYEAR NDATE SHPMNT STATNUM;
  /* Build unique filename pieces. */
  call symput('DYEAR', 'Y'||substr(dyear, 3, 2));
  t = symget('SYSTIME');
  call symput('CTIME', 'T'||substr(t, 1, 2)||substr(t, 4, 2));
  call symput('NDATE', 'D'||left(put("&SYSDATE"D, mmddyy6.)));
  u = symget('SYSJOBID');
  call symput('USER', substr(u, 1, 3));
  call symput('SHPMNT', shipno);
  call symput('STATNUM', statenum);
run;

options NOsource;
*10/04/2000 tbe2 added new extension to final bf19 filename;
*to distinguish files from year to year;
*02/26/2002 tbe2 added new extension to bf19 filename;
*so that if 2 coder code different parts of the same shipment;
*they both can transfer file added userid, date and time;
*03/10/2003 tbe2 changed the filename again because it was too long ;
*for vscpsamp;
*and could not be adjudicated without first renaming the file: took ;
*off 'chss' from;
*morchss, made datayear 'y' + 2 digits, and changed the date to all ;
*numeric and took off the number at the end of the userid; 
options source;

 /* Note: user must have permission to write to BF19 */
/* DEBUG */
***filename NEWBF "BF19.MOR.B&SHPMNT&STATNUM..&USER..&DYEAR..&NDATE..&CTIME" LRECL=476 DISP=NEW ;
filename NEWBF "BQH0.BYTES476" LRECL=476 DISP=OLD;

data _NULL_;
  set work.trial;
  file NEWBF notitles;
  /* The KE3 process uses its own meaning in positions 1-2 and 77-78 which
   * are different from the normal NCHS format.
   */
  put shipno $ 1-2  certno $ 5-10  sex $ 48  dmo $ 49-50  dday $ 51-52  
      age $ 64-66  birthmo $ 67-68  birthdy $ 69-70  bstate $ 74-75  
      typlac $ 76  statenum $ 77-78  dcounty $ 79-81  marital $ 82 
      stres $ 89-90  cntyres $ 91-93  hispanic $ 94  race $ 95  
      educ $ 96-97  injury $ 117  dyear $ 135-138  byear $ 139-142
      bcountryFIPS $ 143-144  bstateFIPS $ 145-146  stocc $ 147-148
      dcountyFIPS $ 149-151  countryresFIPS $ 152-153  
      stateresFIPS $ 154-155  countyresFIPS $ 156-158
      placeresFIPS $ 159-163  bstateFIPS $ 164-165
      mrckbox1 $ 166  mrckbox2 $ 167  mrckbox3 $ 168  mrckbox4 $ 169  
      mrckbox5 $ 170  mrckbox6 $ 171 mrckbox7 $ 172  mrckbox8 $ 173  
      mrckbox9 $ 174  mrckbox10 $ 175  mrckbox11 $ 176 
      mrckbox12 $ 177  mrckbox13 $ 178  mrckbox14 $ 179  mrckbox15 $ 180 
      mr16 $ 181-210  mr17 $ 211-240  mr18 $ 241-270  mr19 $ 271-300 
      mr20 $ 301-330  mr21 $ 331-360  mr22 $ 361-390  mr23 $ 391-420
      ;
run;


endrsubmit;
signoff cdcjes2;
