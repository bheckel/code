//BQH0PREN JOB (BF00,BX21),BQH0,MSGCLASS=K,TIME=2,CLASS=D,REGION=0M             
//STEP1    EXEC SAS,TIME=100,OPTIONS='MEMSIZE=0'                        
//WORK     DD SPACE=(CYL,(100,100),,,ROUND)                                     
//IN00     DD DISP=SHR,DSN=BF19.PAX0024.NATMER
//IN01     DD DISP=SHR,DSN=BF19.PAX0133.NATMER2
//IN02     DD DISP=SHR,DSN=BF19.PAX0223.NATMER
//SYSIN    DD *                                                                 

options nosource;
 /*---------------------------------------------------------------------
  *     Name: PAPRENAT
  *
  *  Summary: Create a table of mother's race by county of occ where
  *           prenatal visit eq '-'.  Do for 2000-2002.  For Francine.
  *
  *           Additional request to provide Not Classif '-' as a percent of
  *           total records for that county of occ.
  *
  *  Created: Wed 26 Feb 2003 14:44:14 (Bob Heckel)
  *---------------------------------------------------------------------
  */
options source linesize=256 NOcenter;

 /* DEBUG */
***filename IN00 '../BF19.ZZX0099.NATMER' LRECL=787;
***filename IN01 '../BF19.ZZX0199.NATMER' LRECL=787;
***filename IN02 '../BF19.ZZX0299.NATMER' LRECL=787;

proc format;
  value $f_race '1'       = 'White (1)'
                '2'       = 'Black (2)'
                '3'       = 'Indian (3)'
                '4'       = 'Chinese (4)'
                '5'       = 'Japanese (5)'
                '6'       = 'Hawaiian (6)'
                '7'       = 'Filipino (7)'
                '8'       = 'Other (8)'
                'A' - 'E' = 'Other API (A-E)'
                'F'       = 'Multi-racial (F)'
                '0'       = 'Other Entries (0)'
                '9'       = 'Not Reported (9)'
                OTHER     = 'do not know (OTHER)';
run;

%macro Iterate;
  %local i;
  %do i=0 %to 2;
    data work.natal&i;
      infile IN0&i MISSOVER;
      input @18 ctyocc $char3.  @44 race $char1.  @66 pncmonth $char1.
            @224 yearbirth $char4.
            ;
    run;
  %end;
%mend Iterate;
%Iterate

%macro Concatenate;
  %local i;
  %global dsnames;
  %do i=0 %to 2;
    %let dsnames=&dsnames work.natal&i;
  %end;
%mend Concatenate;
%Concatenate

title "Using merged files: ";
title2 "BF19.PAX0024.NATMER BF19.PAX0133.NATMER2 BF19.PAX0223.NATMER";

data work.all;
  set &dsnames;
run;


proc tabulate data=work.all format=comma8.;
  format race $f_race.;
  label race="Mother's Race" 
        ctyocc='County of Occ'
        yearbirth='Year of Birth'
        pncmonth='Month PNC Began'
        ;
  class ctyocc race pncmonth yearbirth;
  table ctyocc, race*pncmonth, yearbirth*(N PCTN<pncmonth>) / RTS=21;
run;

endsas;
/***sample output

To verify it, for example using 092, you add
11+23+41+28+7+4+6+2 = 122
then divide
11/122 = .09
the percentage appears to be correct.

 County of Occ 092                                                              
 ---------------------------------------------------------------------------    
 |                   |                    Year of Birth                    |    
 |                   |-----------------------------------------------------|    
 |                   |      2000       |      2001       |      2002       |    
 |                   |-----------------+-----------------+-----------------|    
 |                   |   N    |  PctN  |   N    |  PctN  |   N    |  PctN  |    
 |-------------------+--------+--------+--------+--------+--------+--------|    
 |Mother's |Month PNC|        |        |        |        |        |        |    
 |Race     |Began    |        |        |        |        |        |        |    
 |---------+---------|        |        |        |        |        |        |    
 |Other    |-        |      11|       9|       7|       5|      35|      24|    
 |Entries  |---------+--------+--------+--------+--------+--------+--------|    
 |(0)      |0        |       1|       1|       5|       3|       1|       1|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |1        |      23|      19|      22|      15|      19|      13|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |2        |      41|      33|      44|      30|      37|      26|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |3        |      28|      23|      30|      21|      26|      18|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |4        |       7|       6|      14|      10|      15|      10|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |5        |       4|       3|      11|       8|       4|       3|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |6        |       6|       5|       8|       6|       2|       1|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |7        |       1|       1|       2|       1|       3|       2|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |8        |       1|       1|       1|       1|       2|       1|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |9        |       .|       .|       1|       1|       .|       .|    
 |---------+---------+--------+--------+--------+--------+--------+--------|    
 |White (1)|-        |     431|       4|     479|       5|   1,204|      13|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |0        |     112|       1|     119|       1|     109|       1|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |1        |   2,576|      25|   2,278|      23|   1,758|      19|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |2        |   3,892|      38|   3,642|      38|   3,209|      35|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |3        |   1,696|      17|   1,726|      18|   1,532|      17|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |4        |     636|       6|     615|       6|     595|       7|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |5        |     344|       3|     367|       4|     284|       3|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |6        |     255|       3|     200|       2|     165|       2|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |7        |     121|       1|     149|       2|     140|       2|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |8        |      87|       1|     101|       1|      88|       1|    
 |         |---------+--------+--------+--------+--------+--------+--------|    
 |         |9        |      35|       0|      22|       0|      27|       0|    
 ---------------------------------------------------------------------------    
***/
